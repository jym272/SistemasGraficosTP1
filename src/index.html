<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Aprendiendo WebGl</title>

    <script id="vertex-shader" type="x-shader/x-vertex">
        #version 300 es
        precision mediump float;

        uniform mat4 uModelViewMatrix;
        uniform mat4 uProjectionMatrix;
        uniform mat4 uNormalMatrix;
        uniform vec3 uLightPosition;
        uniform vec4 uMaterialDiffuse;
        uniform bool uWireframe;
        uniform bool uUpdateLight;

        in vec3 aVertexPosition;
        in vec3 aVertexNormal;
        in vec4 aVertexColor;

        out vec3 vNormal;
        out vec3 vLightRay;
        out vec3 vEyeVector;
        out vec4 vFinalColor;

        void main(void) {
            // If wireframe is enabled, set color to the diffuse property exclusing lights
            if (uWireframe) {
                vFinalColor = uMaterialDiffuse;
            }

            vec4 vertex = uModelViewMatrix * vec4(aVertexPosition, 1.0);
            vec4 light = vec4(uLightPosition,1.0);

            // If true, then ensure that light position
            // is appropruately updated
            if (uUpdateLight) {
                light = uModelViewMatrix * vec4(uLightPosition,1.0);
            }

            vNormal = vec3(uNormalMatrix * vec4(aVertexNormal, 1.0));
            vLightRay = vertex.xyz-light.xyz;
            vEyeVector = -vec3(vertex.xyz);
            gl_Position = uProjectionMatrix * uModelViewMatrix * vec4(aVertexPosition, 1.0);
        }
    </script>

    <script id="fragment-shader" type="x-shader/x-fragment">
        #version 300 es
        precision mediump float;

        uniform bool uWireframe;
        uniform vec4 uLightAmbient;
        uniform vec4 uLightDiffuse;
        uniform vec4 uLightSpecular;
        uniform vec4 uMaterialAmbient;
        uniform vec4 uMaterialDiffuse;
        uniform vec4 uMaterialSpecular;
        uniform float uShininess;

        in vec3 vNormal;
        in vec3 vLightRay;
        in vec3 vEyeVector;
        in vec4 vFinalColor;

        out vec4 fragColor;

        void main(void) {
            if (uWireframe) {
                fragColor = vFinalColor;
            }
            else {
                vec3 L = normalize(vLightRay);
                vec3 N = normalize(vNormal);

                float lambertTerm = dot(N, -L);

                // Ambient
                vec4 Ia = uLightAmbient * uMaterialAmbient;
                // Diffuse
                vec4 Id = vec4(0.0, 0.0, 0.0, 1.0);
                // Specular
                vec4 Is = vec4(0.0, 0.0, 0.0, 1.0);

                if (lambertTerm > 0.0) {
                    Id = uLightDiffuse * uMaterialDiffuse * lambertTerm;
                    vec3 E = normalize(vEyeVector);
                    vec3 R = reflect(L, N);
                    float specular = pow( max(dot(R, E), 0.0), uShininess);
                    Is = uLightSpecular * uMaterialSpecular * specular;
                }

                fragColor = vec4(vec3(Ia + Id + Is), 1.0);
            }
        }
    </script>
</head>
<body>
    <canvas id="webgl-canvas" width="800" height="600">
      Your browser does not support the HTML5 canvas element.
    </canvas>

</body>

</html>

